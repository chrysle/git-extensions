#!/bin/bash
set -o pipefail

exec hub-cr --format '%au %as%n' "$@" | awk \
    -F ',? +' \
    -v "me=$(git me-in-github)" \
'
function join(array, start, end, sep,    result, i)
{
    if (sep == "")
	sep = " "
    else if (sep == SUBSEP) # magic value
	sep = ""
    result = array[start]
    for (i = start + 1; i <= end; i++)
	result = result sep array[i]
    return result
}
function nameOrOtherCount(assignees, offset, prefix,      num, result, key, joiner) {
    num = length(assignees) + offset
    if (num <= 0) {
	return ""
    } else if (num <= 2) {
	result = prefix
	for (key in assignees) {
	    if (key == me) continue
	    result = result joiner key
	    joiner = ", "
	}
	return result
    } else {
	return prefix num " other" (num == 1 ? "" : "s")
    }
}
function stats(what, count, mineCount, assignedCount, assignedToUsCount, assignedToMeCount, otherAssignees, coAssignees,        aCnt, additionals) {
    if (mineCount) additionals[aCnt++] = (count == mineCount ? "all" : mineCount) " from me"
    if (assignedCount) {
	additionals[aCnt++] = assignedCount " assigned" nameOrOtherCount(otherAssignees, 0, " and ")
	if (assignedToUsCount) additionals[aCnt++] = (assignedToUsCount == assignedCount ? "all" : assignedToUsCount) " to me" nameOrOtherCount(coAssignees, -1, " and ")
	if (assignedToMeCount) additionals[aCnt++] = (assignedToMeCount == assignedCount ? "all" : assignedToMeCount) " to me"
    }

    return sprintf("%s %s%s%s", \
	(count ? count : "no"), what, (count == 1 ? "" : "s"), \
	(aCnt ? " (" join(additionals, 0, --aCnt, ", ") ")" : "") \
    )
}
function isAssignedAlsoToMe(otherAssignees, coAssignees,     i) {
    for (i = 3; i <= NF; i++) {
	if ($i == me && NF > 3) {
	    recordAssignees(coAssignees)
	    return 1
	}
    }
    recordAssignees(otherAssignees)
    return 0
}
function isAssignedOnlyToMe(    i) {
    return (NF == 3 && $3 == me)
}
function recordAssignees(assignees) {
    for (i = 3; i <= NF; i++) {
	assignees[$i] = 1
    }
}

$1 == "pull" {
    pullCnt += 1
    if ($2 == me) pullMineCnt+=1
    if ($3) {
	pullAssignCnt += 1
	pullToUsCnt += isAssignedAlsoToMe(pullOtherAssignees, pullCoAssignees)
	pullToMeCnt += isAssignedOnlyToMe()
    }
}
$1 == "issue" {
    issueCnt += 1
    if ($2 == me) issueMineCnt+=1
    if ($3) {
	issueAssignCnt += 1
	issueToUsCnt += isAssignedAlsoToMe(issueOtherAssignees, issueCoAssignees)
	issueToMeCnt += isAssignedOnlyToMe()
    }
}
END {
    printf("%s, %s\n", \
	stats("issue", issueCnt, issueMineCnt, issueAssignCnt, issueToUsCnt, issueToMeCnt, issueOtherAssignees, issueCoAssignees), \
	stats("pull request", pullCnt, pullMineCnt, pullAssignCnt, pullToUsCnt, pullToMeCnt, pullOtherAssignees, pullCoAssignees) \
    )
}
'
